function bind_tab --description "'**' autocompletes paths with fd/fzf"
    set -l tokens (commandline --tokenize --current-process)
    # support either '**' or a path like '<optional prefix>/**'
    if string match --quiet -r '^(?<dir>.*/)?\*\*$' -- $tokens[-1]
        if contains -- "$tokens[-2]" cd rmdir pushd
            set fdargs --type d
            set fzfargs --no-multi
        else
            set fdargs ""
            set fzfargs --multi
        end

        # eval is used to do shell expansion on paths
        set dir (eval "printf '%s' $dir")

        # copy/pasted from 'functions fzf-cd-widget'
        test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
        set -lx FZF_DEFAULT_OPTS "--height $FZF_TMUX_HEIGHT --reverse $FZF_DEFAULT_OPTS $FZF_ALT_C_OPTS"

        set -l result (fd --hidden --follow $fdargs . $dir | eval (__fzfcmd) $fzfargs)

        if [ -n "$result" ]
            commandline --current-token -- (string join ' ' (string escape $result))
        end
    else
        commandline --function complete
    end
end

function fish_user_key_bindings
    fzf_key_bindings

    for mode in default insert
        bind -M $mode รง fzf-cd-widget
        bind -M $mode \t 'bind_tab; commandline -f repaint'
    end
end

# https://fishshell.com/docs/current/#command-line-editor
function fish_hybrid_key_bindings --description "Vi-style bindings that inherit emacs-style bindings in all modes"
    for mode in default insert visual
        fish_default_key_bindings -M $mode
    end
    fish_vi_key_bindings --no-erase
end
set -g fish_key_bindings fish_hybrid_key_bindings
