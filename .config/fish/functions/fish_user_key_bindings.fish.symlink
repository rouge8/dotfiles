# bash-like !!
# ref: https://github.com/fish-shell/fish-shell/issues/288#issuecomment-591679913
function bind_bang --description 'bash-like !!'
    switch (commandline --current-token)[-1]
        case '!'
            # Without the `--`, the functionality can break when completing
            # flags used in the history (since, in certain edge cases
            # `commandline` will assume that *it* should try to interpret
            # the flag)
            commandline --current-token -- $history[1]
            commandline --function repaint
        case '*'
            commandline --insert !
    end
end

function bind_tab --description "'**' autocompletes paths with fd/fzf"
    set -l tokens (commandline --tokenize)
    # support either '**' or a path like '<optional prefix>/**'
    if string match -r '^(?<dir>.*/)?\*\*$' -- $tokens[-1]
        if contains -- "$tokens[-2]" cd rmdir pushd
            set fdargs --type d
        else
            set fdargs ""
        end

        # eval is used to do shell expansion on paths
        set dir (eval "printf '%s' $dir")

        # copy/pasted from 'functions fzf-cd-widget'
        test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
        set -lx FZF_DEFAULT_OPTS "--height $FZF_TMUX_HEIGHT --reverse $FZF_DEFAULT_OPTS $FZF_ALT_C_OPTS"

        fd --hidden --follow $fdargs . $dir | eval (__fzfcmd) +m | read -l result

        if [ -n "$result" ]
            # quote the path to handle spaces/etc
            commandline --current-token -- '"'$result'"'
        end
    else
        commandline --function complete
    end
end

function fish_user_key_bindings
    fzf_key_bindings

    for mode in default insert
        bind -M $mode '!' bind_bang
        bind -M $mode รง fzf-cd-widget
        bind -M $mode \t 'bind_tab; commandline -f repaint'
    end
end

# https://fishshell.com/docs/current/#command-line-editor
function fish_hybrid_key_bindings --description "Vi-style bindings that inherit emacs-style bindings in all modes"
    for mode in default insert visual
        fish_default_key_bindings -M $mode
    end
    fish_vi_key_bindings --no-erase
end
set -g fish_key_bindings fish_hybrid_key_bindings
